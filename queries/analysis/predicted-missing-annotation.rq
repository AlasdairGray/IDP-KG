#+ summary: Proteins with predicted disorder but no manual annotation
#+ description: Which proteins have predicted disordered regions in MobiDB, but are not yet annotated manually in DisProt. The same for PED.
#+ endpoint: https://swel.macs.hw.ac.uk/data/repositories/idpkg
#+ tags:
#+   - Analysis

PREFIX pav: <http://purl.org/pav/>
PREFIX schema: <https://schema.org/>
PREFIX void: <http://rdfs.org/ns/void#>

SELECT DISTINCT ?protein (SAMPLE (?name) AS ?pName) (GROUP_CONCAT (?identifier;SEPARATOR=',') AS ?sourceIDs)
WHERE {
    {
        GRAPH ?mobidb {
            ?protein a schema:Protein .
            OPTIONAL {?protein schema:identifier ?identifier }
            OPTIONAL {?protein schema:name ?name }
        }
        ?mobidb void:inDataset "https://mobidb.org/#2020-09" .
    } UNION {
        GRAPH ?ped {
            ?protein a schema:Protein .
            OPTIONAL {?protein schema:identifier ?identifier }
            OPTIONAL {?protein schema:name ?name }
        }
        ?ped void:inDataset "https://proteinensemble.org/#2021-02-12" .
    } MINUS {
        GRAPH ?disprot {
            ?protein a schema:Protein
        }
        ?disprot void:inDataset "https://disprot.org/#2021-08" .
    }
}
GROUP BY ?protein