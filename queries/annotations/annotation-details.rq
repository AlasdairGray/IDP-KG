# Sequence Annotation Information
# Return information known about each sequence annotation.

PREFIX schema: <https://schema.org/>

SELECT ?s ?annotation ?start ?end ?termCode ?termName ?pubmedID
WHERE {
    graph ?g {
        ?s a schema:Protein;
           schema:hasSequenceAnnotation ?annotation .
        ?annotation schema:additionalProperty/schema:value ?term;
            schema:sequenceLocation ?range .
        ?range schema:rangeStart ?start ;
               schema:rangeEnd ?end .
        ?term schema:termCode ?termCode ;
            schema:name ?termName .
        OPTIONAL { ?annotation schema:subjectOf ?pubmedID }
    }
}    
ORDER BY ?s ?start ?end
