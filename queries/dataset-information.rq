# Query to retrieve protein data
# Defensive query: assumes that data does not conform to Protein profile
# Retrieve all properties that are mentioned in the Protein Profile
# Proteins are grouped by their IRI, with multiple values possible

PREFIX schema: <https://schema.org/>
PREFIX pav: <http://purl.org/pav/>
SELECT  ?s
	(GROUP_CONCAT(DISTINCT ?description;SEPARATOR=',<br/>') AS ?descriptions)
	(GROUP_CONCAT(DISTINCT ?identifier;SEPARATOR=',<br/>') AS ?identifiers)
	(GROUP_CONCAT(DISTINCT ?keywords;SEPARATOR=',<br/>') AS ?keywordss)
	(GROUP_CONCAT(DISTINCT ?name;SEPARATOR=',<br/>') AS ?names)
	(GROUP_CONCAT(DISTINCT ?url;SEPARATOR=',<br/>') AS ?urls)
	(GROUP_CONCAT(DISTINCT ?citation;SEPARATOR=',<br/>') AS ?citations)
	(GROUP_CONCAT(DISTINCT ?creator;SEPARATOR=',<br/>') AS ?creators)
	(GROUP_CONCAT(DISTINCT ?distribution;SEPARATOR=',<br/>') AS ?distributions)
	(GROUP_CONCAT(DISTINCT ?includedInDataCatalog;SEPARATOR=',<br/>') AS ?catalogs)
	(GROUP_CONCAT(DISTINCT ?license;SEPARATOR=',<br/>') AS ?licenses)
	(GROUP_CONCAT(DISTINCT ?measurementTechnique;SEPARATOR=',<br/>') AS ?measurementTechniques)
	(GROUP_CONCAT(DISTINCT ?variableMeasured;SEPARATOR=',<br/>') AS ?variablesMeasured)
	(GROUP_CONCAT(DISTINCT ?version;SEPARATOR=',<br/>') AS ?versions)
	(GROUP_CONCAT(DISTINCT ?sameAs;SEPARATOR=',<br/>') AS ?equivalences)
	(GROUP_CONCAT(DISTINCT ?source;SEPARATOR=',<br/>') AS ?sources)
WHERE {
	GRAPH ?g {
## Bioschemas Minimal Properties
		?s a schema:Dataset .
		OPTIONAL {?s schema:description ?description}
		OPTIONAL {?s schema:identifier ?identifier }
		OPTIONAL {?s schema:keywords ?keywords}
		OPTIONAL {?s schema:name ?name }
		OPTIONAL {?s schema:url ?url}
## Bioschemas Recommended properties
		OPTIONAL {?s schema:citation ?citation}
		OPTIONAL {?s schema:creator ?creator}
		OPTIONAL {?s schema:distribution ?distribution}
		OPTIONAL {?s schema:includedInDataCatalog ?includedInDataCatalog }
		OPTIONAL {?s schema:license ?license }
		OPTIONAL {?s schema:measurementTechnique ?measurementTechnique}
		OPTIONAL {?s schema:variableMeasured ?variableMeasured}
		OPTIONAL {?s schema:version ?version}
## Others
		OPTIONAL {?s schema:sameAs ?sameAs }
	}
	?g pav:retrievedFrom ?source
}
GROUP BY ?s
