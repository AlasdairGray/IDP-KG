# Minimal protein information query
# Defensive query: assumes that data does not conform to Protein profile
# Retrieve a minimal set of properties that are mentioned in the Protein Profile
# Proteins are grouped by their IRI, with multiple values possible

PREFIX schema: <https://schema.org/>
PREFIX pav: <http://purl.org/pav/>
PREFIX void: <http://rdfs.org/ns/void#>

SELECT  ?s
  (GROUP_CONCAT(DISTINCT ?name;SEPARATOR=',<br/>') AS ?names)
  (GROUP_CONCAT(DISTINCT ?description;SEPARATOR=',<br/>') AS ?descriptions)
  (GROUP_CONCAT(DISTINCT ?identifier;SEPARATOR=',<br/>') AS ?identifiers)
  (GROUP_CONCAT(DISTINCT ?associatedDisease;SEPARATOR=',<br/>') AS ?diseases)
  (GROUP_CONCAT(DISTINCT ?encodedBy;SEPARATOR=',<br/>') AS ?genes)
  (GROUP_CONCAT(DISTINCT ?taxonomicRange;SEPARATOR=',<br/>') AS ?taxons)
  (GROUP_CONCAT(DISTINCT ?sameAs;SEPARATOR=',<br/>') AS ?sames)
  (GROUP_CONCAT(DISTINCT ?source;SEPARATOR=',<br/>') AS ?sources)
  (GROUP_CONCAT(DISTINCT ?dataset;SEPARATOR=',<br/>') AS ?datasets)
WHERE {
    GRAPH ?g {
# Bioschemas Minimal Properties
        ?s a schema:Protein .
        OPTIONAL {?s schema:identifier ?identifier }
        OPTIONAL {?s schema:name ?name }
## Bioschemas Recommended properties
        OPTIONAL {?s schema:associatedDisease ?associatedDisease}
        OPTIONAL {?s schema:description ?description}
        OPTIONAL {?s schema:isEncodedByBioChemEntity ?encodedBy}
        OPTIONAL {?s schema:taxonomicRange ?taxonomicRange }
        OPTIONAL {?s schema:url ?url}
        OPTIONAL {?s schema:sameAs ?sameAs }
    }
    ?g pav:retrievedFrom ?source
    OPTIONAL {?g void:inDataset ?dataset}
}
GROUP BY ?s
