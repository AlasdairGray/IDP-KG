# Minimal Bioschemas protein information query
#+ description: Retrieve a minimal set of properties that are mentioned in the Protein Profile. Proteins are grouped by their IRI, with multiple values possible. Defensive query: assumes that data does not conform to Protein profile
#+ endpoint: https://swel.macs.hw.ac.uk/data/repositories/idpkg
#+ pagination: 100
#+ tags:
#+   - Protein

PREFIX schema: <https://schema.org/>
PREFIX pav: <http://purl.org/pav/>
PREFIX void: <http://rdfs.org/ns/void#>

SELECT  ?s
  (GROUP_CONCAT(DISTINCT ?name;SEPARATOR=',') AS ?names)
  (GROUP_CONCAT(DISTINCT COALESCE(?description, '');SEPARATOR=',') AS ?descriptions)
  (GROUP_CONCAT(DISTINCT ?identifier;SEPARATOR=',') AS ?identifiers)
  (GROUP_CONCAT(DISTINCT COALESCE(?associatedDisease, '');SEPARATOR=',') AS ?diseases)
  (GROUP_CONCAT(DISTINCT COALESCE(?encodedBy,'');SEPARATOR=',') AS ?genes)
  (GROUP_CONCAT(DISTINCT COALESCE(?taxonomicRange,'');SEPARATOR=',') AS ?taxons)
  (GROUP_CONCAT(DISTINCT ?sameAs;SEPARATOR=',') AS ?sames)
  (GROUP_CONCAT(DISTINCT ?source;SEPARATOR=',') AS ?sources)
  (GROUP_CONCAT(DISTINCT ?dataset;SEPARATOR=',') AS ?datasets)
WHERE {
    GRAPH ?g {
# Bioschemas Minimal Properties
        ?s a schema:Protein .
        OPTIONAL {?s schema:identifier ?identifier }
        OPTIONAL {?s schema:name ?name }
## Bioschemas Recommended properties
        OPTIONAL {?s schema:associatedDisease ?associatedDisease}
        OPTIONAL {?seq schema:description ?description}
        OPTIONAL {?s schema:isEncodedByBioChemEntity ?encodedBy}
        OPTIONAL {?s schema:taxonomicRange ?taxonomicRange }
        OPTIONAL {?s schema:url ?url}
        OPTIONAL {?s schema:sameAs ?sameAs }
    }
    ?g pav:retrievedFrom ?source
    OPTIONAL {?g void:inDataset ?dataset}
}
GROUP BY ?s
